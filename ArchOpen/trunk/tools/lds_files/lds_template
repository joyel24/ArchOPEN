ENTRY(entry_code)

MEMORY
{
   DRAM : ORIGIN = @DRAM_START@,    LENGTH = @DRAM_LEN@
   IRAM : ORIGIN = @IRAM_START@,    LENGTH = @IRAM_LEN@-0x4
}

SECTIONS
{
    .text @CODE_START@:
    {
        _code_start = .;
        *(.text.init.arch)
        *(.text.init)
        *(.text)
        *(.glue_7t)
        *(.glue_7)
    } > DRAM

    .rodata :
    {
        *(.rodata)
        *(.rodata.str1.4)
    } > DRAM

    .data :
    {
        *(.data)
        *(.got)
        *(.got.*)
        _real_iram_start = .;
    } > DRAM

    .iram @IRAM_START@ : AT ( _real_iram_start )
    {
        _iram_start = .;
        *(.vectors)
        *(.core)
        *(.cored)
        _iram_end = .;
    } > IRAM

    .sdend @DRAM_START@+@DRAM_LEN@-@END_DRAM_SIZE@ : AT ( _real_iram_start + SIZEOF(.iram) )
    {
        _sdend_start = .;
        *(.fwuncomp_code)
        *(.fwuncomp_data)
        _sdend_end = .;
    } > DRAM

    .bss _real_iram_start (NOLOAD) : AT ( _real_iram_start )
    {
        _bss_start = .;
        *(.bss)
        _bss_end = ALIGN(0x04);
    } > DRAM

    .common :
    {
        *(.common)
        _end_kernel = . ;
    } > DRAM
}